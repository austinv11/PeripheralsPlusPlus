buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        jcenter()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
    }
}

repositories {
    maven {
        url  "http://jcenter.bintray.com"
    }
    maven {
        name = "chickenbones"
        url = "http://chickenbones.net/maven"
    }
}

apply plugin: 'forge'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

ext.buildNumber = System.getenv("BUILD_ID") ?: "dev"
version = "1.3.5-$buildNumber"
//version = "1.3.5"
group= "com.austinv11.peripheralsplusplus" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Peripherals++"

minecraft {
    version = "1.7.10-10.13.4.1558-1.7.10"
    runDir = "eclipse"
    replace("@VERSION@",project.version)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sourceJar
            artifact deobfJar
        }
    }
}

bintray {
    user = 'austinv11'
    key = System.getenv("API_KEY")
    dryRun = false
    publish = true
    publications = ['mavenJava']
    pkg {
        repo = "maven"
        name = "PeripheralsPlusPlus"
        desc = "A ComputerCraft Addon"
        websiteUrl = "https://github.com/austinv11/PeripheralsPlusPlus"
        issueTrackerUrl = "https://github.com/austinv11/PeripheralsPlusPlus/issues"
        vcsUrl = "https://github.com/austinv11/PeripheralsPlusPlus.git"
        licenses = ["GPL v2"]
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
}

dependencies {
    // you may put jars on which you depend on in ./libs
    // or you may define them like so..
    //compile "some.group:artifact:version:classifier"
    //compile "some.group:artifact:version"
      
    // real examples
    //compile 'com.mod-buildcraft:buildcraft:6.0.8:dev'  // adds buildcraft to the dev env
    //compile 'com.googlecode.efficient-java-matrix-library:ejml:0.24' // adds ejml to the dev env

    // Chatter library
    // https://github.com/pierredavidbelanger/chatter-bot-api
    // http://mavenbrowse.pauldoo.com/central/ca/pjer/chatter-bot-api/
    // It's in the ./libs folder, but heres the maven link just in case
    //compile "ca.pjer:chatter-bot-api:1.3.3"

    // for more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html
    compile "com.austinv11.collectiveframework:CollectiveFramework:1.0.0-122:deobf"
    compile "codechicken:NotEnoughItems:1.7.10-1.0.3.74:dev"
	compile "codechicken:CodeChickenLib:1.7.10-1.1.1.99:dev"
	compile "codechicken:CodeChickenCore:1.7.10-1.0.4.29:dev"
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes 'FMLAT': 'ppp_at.cfg',
                'FMLCorePlugin': 'com.austinv11.peripheralsplusplus.asm.PeripheralsPlusPlusTransformerPlugin',
                'FMLCorePluginContainsFMLMod': true
    }
    from zipTree("libs/cmulex.jar")
    from zipTree("libs/en_us.jar")
    from zipTree("libs/freetts.jar")
    from zipTree("libs/freetts-jsapi10.jar")
    from zipTree("libs/mbrola.jar")
    from zipTree("libs/cmu_us_kal.jar")
    // Chatter library
    from zipTree("libs/chatter-bot-api-1.3.3.jar")
}

idea { 
    module { 
        inheritOutputDirs = true 
    } 
}

// add source jar
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'source'
}

// deobf jars
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
    manifest {
        attributes 'FMLAT': 'ppp_at.cfg',
                'FMLCorePlugin': 'com.austinv11.peripheralsplusplus.asm.PeripheralsPlusPlusTransformerPlugin',
                'FMLCorePluginContainsFMLMod': true
    }
}

build.dependsOn sourceJar, deobfJar
